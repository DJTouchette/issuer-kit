// The applicatino label
// This is used to find all of the build configurations associated to the application
// To work the build configurations must have an "app" label;
// - For example; app=iiw-book
def APP_LABEL = 'identity-kit'

// Wait timeout in minutes
def WAIT_TIMEOUT = 10

// Assumes Builds, Images, and Deployments all have the same name(s)
// Components will be deployed in the order they are listed.
def COMPONENTS = ['identity-kit-admin', 'identity-kit-public', 'identity-kit-controller', 'identity-kit-agent', 'identity-kit-db', 'identity-kit-wallet']

// Edit your environment TAG names below
def TAG_NAMES = ['prod']

// Edit your deployment environment names below
def DEP_ENV_NAMES = ['prod']


// The base namespace of you environments.
def NAME_SPACE = 'devex-von'

// Get an image's hash tag
String getImageTagHash(String imageName, String tag = "") {

  if(!tag?.trim()) {
    tag = "latest"
  }

  def istag = openshift.raw("get istag ${imageName}:${tag} -o template --template='{{.image.dockerImageReference}}'")
  return istag.out.tokenize('@')[1].trim()
}

node {
  stage("Building images ...") {
    script {
      openshift.withCluster() {
        openshift.withProject() {

          def BASE_IMAGE_SELECTOR = "${APP_LABEL}-base-image"

          // Find all of the build configurations for the base images, using labels ...
          def baseImageBuildConfigs = openshift.selector("bc", [ "app-group" : "${BASE_IMAGE_SELECTOR}" ])
          echo "Found ${baseImageBuildConfigs.count()} buildconfigs for app-group label (app-group=${BASE_IMAGE_SELECTOR}): ${baseImageBuildConfigs.names()}"

          // Kick off all the base image builds in parallel ...
          def baseImageBuilds = baseImageBuildConfigs.startBuild()
          echo "Started ${baseImageBuilds.count()} builds: ${baseImageBuilds.names()}"

          timeout(WAIT_TIMEOUT) {
            // Wait for all the builds to complete ...
            // This section will exit after the last build completes.
            echo "Waiting for builds to complete ..."
            baseImageBuilds.withEach {
              // untilEach and watch - do not support watching multiple named resources,
              // so we have to feed it one at a time.
              it.untilEach(1) {
                  echo "${it.object().status.phase} - ${it.name()}"
                  return (it.object().status.phase == "Complete")
              }
            }
          }

          def RUNTIME_IMAGE_SELECTOR = "${APP_LABEL}-runtime"

          // Find all of the build configurations for the runtime images, using labels ...
          def runtimeImageBuildConfigs = openshift.selector("bc", [ "app-group" : "${RUNTIME_IMAGE_SELECTOR}" ])
          echo "Found ${runtimeImageBuildConfigs.count()} buildconfigs for app-group label (app-group=${RUNTIME_IMAGE_SELECTOR}): ${runtimeImageBuildConfigs.names()}"

          // Kick off all the base image builds in parallel ...
          def runtimeImageBuilds = runtimeImageBuildConfigs.startBuild()
          echo "Started ${runtimeImageBuilds.count()} builds: ${runtimeImageBuilds.names()}"

          timeout(WAIT_TIMEOUT) {
            // Wait for all the builds to complete ...
            // This section will exit after the last build completes.
            echo "Waiting for builds to complete ..."
            runtimeImageBuilds.withEach {
              // untilEach and watch - do not support watching multiple named resources,
              // so we have to feed it one at a time.
              it.untilEach(1) {
                  echo "${it.object().status.phase} - ${it.name()}"
                  return (it.object().status.phase == "Complete")
              }
            }
          }

          echo "Builds complete ..."
        }
      }
    }
  }

  for(item in COMPONENTS)
  {
    stage("Deploying ${item}") {
      script {
        openshift.withCluster() {
          openshift.withProject() {

            echo "Tagging ${item} for deployment to ${TAG_NAMES[0]} ..."

            // Don't tag with BUILD_ID so the pruner can do it's job; it won't delete tagged images.
            // Tag the images for deployment based on the image's hash
            def IMAGE_HASH = getImageTagHash("${item}")
            echo "IMAGE_HASH: ${IMAGE_HASH}"
            openshift.tag("${item}@${IMAGE_HASH}", "${item}:${TAG_NAMES[0]}")
          }

          openshift.withProject("${NAME_SPACE}-${DEP_ENV_NAMES[0]}") {
              def dc = openshift.selector('dc', "${item}")
              // Wait for the deployment to complete.
              // This will wait until the desired replicas are all available
              dc.rollout().status()
          }

          echo "Deployment Complete."
        }
      }
    }
  }
}